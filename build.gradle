plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'idea'
}

group = 'ee.metsmarko'
version = '1.0.0'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}
idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
    integrationTestCompileOnly.extendsFrom testCompileOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'

    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    integrationTestImplementation 'org.testcontainers:testcontainers:1.17.2'
    integrationTestImplementation 'org.testcontainers:junit-jupiter:1.17.2'
    integrationTestImplementation 'org.testcontainers:postgresql:1.17.2'
    integrationTestImplementation 'org.testcontainers:rabbitmq:1.17.2'
}


task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

tasks.withType(Test) {
    useJUnitPlatform()
}

check.dependsOn integrationTest

bootBuildImage {
    imageName = "metsmarko/tuum-account"
}

checkstyle {
    toolVersion = "10.3.1"
    maxErrors = 0
    maxWarnings = 0
}
